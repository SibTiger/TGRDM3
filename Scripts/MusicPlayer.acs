// Music Player Algorithm
// ----
// This algorithm is designed to automatically rotate through a randomized playlist and change to the next song after so
//  much time has been lapsed.  The overall idea of this feature is to provide a fresher experience overall when playing
//  through each map, thus it doesn't feel like the exact environment each time when the user goes through the maps after so
//  many times.
// This algorithm requires no interaction from the user and is not customizable to the end-user perspective.
// ----



//        << ! ACHTUNG ! >>
// ===============================
// The size of the music play list.
//  If adding more music, please update only this variable and then make the
//  appropriate changes in the SNDINFO using $MusicAlias while keeping the
//  original formatting schematic of 'MUS##'.  Using anything other than 'MUS##'
//  will fail, so you MUST use 'MUS##'.  For example: 'MUS00' or 'MUS39' is legal, but 'MyUltraCoolMP3ExclamationMarkTimes3' will not work.
//   NOTE: When adding more music, the last MUS## number in the SNDINFO, add one
//   more digit.  So when defining 'MusicPlayListSize, if the last MUS in SNDINFO
//   is MUS34, then add one more digit.  Thus, it'll be MusicPlayListSize = 35.
//   Or simply use (PlayList + 1)
#define MusicPlayListSize 54
// If adding music, you do NOT need to edit anything else in this script.
// ----



// Music Play List Array
global str 1: MusicPlayList[];

// PlayList Ready [Generated, shuffled]
global int 2: MusicPlayListReady;

// PlayList Highlight [points to highlighted index]
global int 3: MusicPlayListHighlight;



// Front end Music Player Driver
//  This script merely makes sure that the array is ready for use before
//  jumping into the main operation.
SCRIPT "MusicPlayer" OPEN CLIENTSIDE
{
    // Since this function\script is always going to reload on each map load
    //  we have to make sure that the play list array is ready for use.
    //  We do not need to regenerate the array on each map load and waste
    //  the end-users system resources.  Instead, lets try to optimize it
    //  at the fullest possible.
    if (!MusicPlayListReady)
        // Play list is not ready yet; populate and generate the play list.
        GenerateMusicPlayList();
        
    // Play list is ready to be used
    // Lay down that boogie and play that funky music till you die
    //  Wild Cherry - Play That Funky Music, White Boy
    //  I had too ;)
            ACS_NamedExecute("MusicPlayer_BackEnd", 0, 0, 0, 0);
} // MusicPlayer()



// This script is the main loop operation for the game music.
// This script is a dedicated driver into how the music will interact with the user.
SCRIPT "MusicPlayer_BackEnd" (VOID) CLIENTSIDE
{
    // Declarations and Initializations
        // When running this function for the first time, don't run some features
        //  such as Music Fade Out.
            bool firstRun = true;
        // Fetch the map's LevelNum [ZMAPINFO]
            int levelNum = GetLevelInfo(LEVELINFO_LEVELNUM);
        // A mutable variable that determines if the music player can play depending
        //  on the map's levelnum.
            bool mapBlackListed = MapBlackList(levelNum);
    // ----


    
    
    do
    {
        // Only run the music when the play list is ready, if we're not in the 'TITLEMAP', and if the map is not blacklisted.
        if (MusicPlayListReady && (GameType() != GAME_TITLE_MAP) && !mapBlackListed)
        {
            // Fade out current music
            //  but only if we're not running this function for the first time.
                if (!firstRun)
                    // Fade out the music
                    ACS_NamedExecuteWait("MusicFade", 0, 0, 17, 0);
                else
                    // Turn off 'First Run'.
                    firstRun = false;
            // Change the song
                SetGameMusic(MusicPlayListHighlight);
            // Fade in the next song
                ACS_NamedExecuteWait("MusicFade", 0, 1, 17, 0);
            // Wait for ~ 1min and 30secs.
                Delay(35*90);
            // Move the Music Play List Highlight to the next index.
                UpdatePlayListHighlight();
            // If the highlighted index is now '0', regenerate the playlist.
                if (!MusicPlayListHighlight)
                    GenerateMusicPlayList();
        } // if (play list is ready)
        
        else
            // To prevent a RunAway fault, issue a small pause.
            Delay(1);
    } while (1);
} // MusicPlayer_BackEnd()



// This function will avoid the music player from evening playing
//  if the map is blacklisted from this feature.  Blacklisting is
//  useful if the map has its own desired music and is not intended
//  to use this very feature.
// For the list of LevelNum's, please see the ZMAPINFO under GameMaps
function bool MapBlackList (int levelNum)
{
    switch (levelNum)
    {
        case 99:
        // Introduction Map
        return True;
        break;
    } // switch()
    
    // Not blacklisted
    return False;
} // MapBlackList()



// This is a dedicated function to increment the array highlighter,
//  but also makes sure that the highlighter stays within the spectrum
//  of the array and not outside.
function void UpdatePlayListHighlight (void)
{
    // Make sure that the highlighter stays inside the array.
    if (MusicPlayListHighlight >= (MusicPlayListSize - 1))
        // The highlighter is out of boundary; start back to zero.
        MusicPlayListHighlight = 0;
    else
        // Increment the highlighter to the index.
        MusicPlayListHighlight++;
} // UpdatePlayListHighlight()



// Make the play list array ready for use.
//  Build up the array and shuffle it.
function void GenerateMusicPlayList (VOID)
{
    // Mark the playlist as not ready for use.
        MusicPlayListReady = false;
    // Auto-fill the array
        MusicPlayList_AutoFill();
    // Shuffle the playlist
        MusicPlayer_ShufflePlayList();
    // Music play list is now ready for use
        MusicPlayListReady = true;
} // GenerateMusicPlayList()



// This function is designed to take the music array and randomly reorganize it.
// We're going to use the Fisher-Yates Shuffle Modern Algorithm
//  We start from the ending of the array and simply shuffle the array from
//  the beginning to the tail ending of the array list.  In the next pass, we'll
//  shift from the tail ending to the index before the tail ending, and do the
//  routine again.  We continue this routine until we hit index 0.  This is an
//  unbias algorithm and should be efficient enough.  Although with ZDoom's Random
//  ACS function, I can't guarantee the best randomization results.
// Algorithm: https://en.wikipedia.org/wiki/Fisher%E2%80%93Yates_shuffle
function void MusicPlayer_ShufflePlayList (void)
{
    // Declarations needed for the algorithm
        // Used for randomly selected indexes
            int n;
        // Keeps the value of the highlighted index
            int temp;
    // ----

    // Randomization algorithm
    for (int i = (MusicPlayListSize - 1); i > 0; --i)
    {
        // Fetch a random index to switch with another index.
            n = Random(0,i);
        
        // Only switch index values if and only if the randomly selected
        //  index is NOT the highlighted index itself.
        if (n != i)
        {
            // Save the highlighted index (not randomized) to a variable
                temp = MusicPlayList[i];
            // Switch the randomly selected index with the highlighted index.
                MusicPlayList[i] = MusicPlayList[n];
            // Replace the randomly selected index's value with the highlighted index.
                MusicPlayList[n] = temp;
        } // if
    } // for
} // ReorganizeArray()



// This function is designed to fade the music in or out.
//  This is illusion of crossfading between one one song to another.
//  This merely isn't a perfect implementation, but - it's better than
//  shifting to something soft and easy listening like Marvin Gaye and
//  immediately you notice your ears are bleeding with Mudvayne screaming
//  in your ears.  So, with this fade - you'll mentally accept the song
//  transitioning.
//
// FIXED POINT NUMBERS WARNING {Because I hate them; READ THIS}
//  http://zdoom.org/wiki/Fixed_point_number
// ----
// Parameters:
//  fadeType <int>
//      Determines the type of fading.
//      0 = Fade out; stop music
//      1 = Fade in; play music
//  faderSpeed <int>
//      The tic-rate in which to fade the music in or out.
//      Recommended value: 17tics
SCRIPT "MusicFade" (int fadeType, int faderSpeed) CLIENTSIDE
{
    // Declarations
        // This variable is going to be used as a base in terms of
        //  fading in or out.
            int fixedFader;
    // ----

        // Fade out
        if (!fadeType)
        {
            // Update the variable.
            fixedFader = 1.0;
            
            // Fade out effect
            do
            {
                SetMusicVolume(fixedFader);
                fixedFader -= 0.1;
                delay(faderSpeed);
            } while(fixedFader >= 0.0);
        } // if (fadeType = 0)
        
        
        // Fade in
        if (fadeType)
        {
            // Unfortunately, due to a slight limitation with 'SetMusicVolume()', I had to comment this entire
            //  feature out.  When 'SetMusic()' has been invoked and 'SetMusicVolume()' has been set to
            //  a value of '0.0', the function 'SetMusic()' will ignore the 'SetMusicVolume()' value and reset it.
            //  Topic: http://forum.zdoom.org/viewtopic.php?p=908222#p908222
            
            // To be sure - if incase - there is changes with ZDoom's music functionality in the future,
            //  forcefully revert the SetMusicVolume() value.
                fixedFader = 1.0;
                SetMusicVolume(fixedFader);
            
            
            /*
            // Update the variable.
            fixedFader = 0.1;
            
            // Fade in effect
            do
            {
                SetMusicVolume(fixedFader);
                fixedFader += 0.1;
                delay(faderSpeed);
            } while (fixedFader <= 1.0);
            */
        } // if (fadeType = 1)
} // MusicFade()



// Sets the music for all clients to the desired song.
// ----
// Parameters:
//  musicIndex <int>
//      The selected index from the play list array.
function void SetGameMusic (int musicIndex)
{
    SetMusic(MusicPlayList[musicIndex]);
} // SetGameMusic()



// Auto-fill the play list array with 'MUS$num' formatting
//  Where $num is the size of the array, in which is dependent on the variable
//  'MusicPlayListSize' <int>.
function void MusicPlayList_AutoFill (VOID)
{
    // Automatically fill up the array with 'MUS##'.
    // For example: MUS00, MUS01, MUS02, etc.
    // The limit is defined by (MusicPlayListSize - 1)
        for (int i = 0; i < MusicPlayListSize; i++)
            MusicPlayList[i] = StrParam(s:"MUS", i:i);
} // MusicPlayList_AutoFill()