// Domination Adapt Algorithm
// ----
// This script is designed to automatically detect the game mode environment, by
//  checking if the Domination game mode [ http://wiki.zandronum.com/Game_Modes#Domination ]
//  is set as the game mode or if the game is set to the Deathmatch game mode.
//  If this script detects that the Domination game mode was detected, then this will maps
//  to adjust themselves accordingly or even activate THINGs if necessary.
// Remember that the SECTINFO manages what sectors are important for the gamemode.
//  Anything else, the SkullTag\Zandronum engine would take care of by itself.
//  There's no extra work! :)
// ----



// Domination Check
// ----
// Description
//  This function tries to detect if we're in the Domination game mode.
//  If we're in the domination game mode, this function will return the 
//  appropriate value.
// Output
//  True = Domination is presumed to be running.
//  False = Domination was not detected; presumed Deathmatch
function bool Domination_Check (void)
{
    // Try to detect the game mode
    // Do keep in mind, that ZDoom does NOT support the CVar 'Domination' nor
    //  does it have 'GAME_NET_TEAMGAME', so we have to use the longer method
    //  to get the same result as 'GAME_NET_TEAMGAME' but under assumption.
    // NOTE BUG
    //  If the game mode is CTF, SkullTag, or anything of the like that nests in 'GAME_NET_TEAMGAME', this still activate the Domination game setting.
    //  There's no other way around this issue, this is just a limitation with
    //  the engines and staying compatible between both Zandronum and GZDoom.
    IF (GameType() != GAME_NET_DEATHMATCH &&
        GameType() != GAME_NET_COOPERATIVE &&
        GameType() != GAME_SINGLE_PLAYER &&
        GameType() != GAME_TITLE_MAP)
        // Domination
        Return True;

    // Anything other than the Domination Game Mode
    Return False;
} // Domination_Check()



// This script is designed to toggle the Ring Spawner actors to their appropriate settings.
//  If the Domination game mode was detected, then the Ring Spawners will be activated, however
//  they will remain deactivated or will be deactivated upon change.
// Despite currently the game mode can not easily be set on the fly, so this check may feel redundant
//  upon checking within a loop.  However, the engine might be able to handle these techniques differently
//  in the future.  So, this game must be able to cooperate with that expectancy - if required.
//
// NOTE: The Ring Spawners have a static THING tag of 9999997
//  This makes activation and manipulation far easier.
#define ringSpawnerTag 9999997
SCRIPT "Toggle_RingSpawner" OPEN
{
    // Check the game mode and toggle the spawners to their appropriate activation settings.
    do
    {
        if (Domination_Check())
            // Domination game mode was detected.
            Thing_Activate(ringSpawnerTag);
        else
            // Something else was detected, make sure the spawners are off.
            Thing_Deactivate(ringSpawnerTag);
        // Wait for approx. one second before checking again.
        delay(35);
    } while (true);
} // Toggle_RingSpawner()