Actor TGRGrenadeLauncher : DoomWeapon 5011
{
    // GZDoom Builder Configuration
    // ==============================
        //$Category "Weapons"
        //$Sprite GLAUA0
        //$Title "Grenade Launcher"
    // ------------------------------
    // ==============================


    Game Doom
    Weapon.SelectionOrder 6000
    Radius 20
    Height 16
    Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER"
    Tag "Grenade Launcher"
    // Standards
    // ===========
        +Inventory.Untossable // Can not 'drop (CCMD)' the weapon
        +Inventory.Undroppable // Weapon can never be taken away from the player's inventory.
        +Weapon.NoAutoAim // Disallow AutoAim with this weapon.
        +Weapon.No_Auto_Switch // When this weapon is picked up, do not switch to it.
        Weapon.BobStyle InverseSmooth // Different taste of bobbing; use Doom Alpha's bobbing style - but smooth.
        +ForceXYBillboard // Usable only for when Voxels is disabled and using Sprites.

        // Because some mods in Zandronum abuse the ConsoleCommand to change the user's settings; I found one that changes bobbing....
            Weapon.BobRangeX 1.0
            Weapon.BobRangeY 1.0
            Weapon.BobSpeed  1.0
            Weapon.YAdjust 10
    // ------
    // ======

    // ----
    // Flags, Switches, and Properties
    // =================================
        Inventory.RespawnTics 175 // Change the respawn time to 5 seconds [Quake Live inspired]
        +Inventory.InvBar // Show the weapon icon in the HUD Inventory list
        -Weapon.NoAutoFire // Allow the weapon to automatically fire (if the user still has +attack == true) when raised or selected.
        Weapon.PreferredSkin "Grenade-Marine" // Zandronum feature only; dependency required: MarineSkins
        +NoGravity
        +FloatBob
    // ------
    // ======

    // ----
    // Ammo Management
        Weapon.AmmoUse 1
        Weapon.AmmoGive 8
        Weapon.AmmoType "RocketAmmo"
    // ----
        Inventory.Icon "GLAUA0"
    // ------
    States
    {
    Spawn:
        // Allow a function to be called immediately
            TNT1 A 0 NoDelay
        // Spawn the item, but raise it by z-offset value
            GLAU A -1 A_Warp(AAPTR_DEFAULT, 0, 0, 8)
        Stop
    Ready:
        GRLG A 1 A_WeaponReady
        Loop
    Deselect:
        GRLG A 0 A_Lower // Lower the weapon faster
        GRLG A 1 A_Lower
        Loop
    Select:
        GRLG A 0 A_Raise // Raise the weapon faster
        GRLG A 1 A_Raise
        Loop
    Fire: 
        GRLG B 8 A_GunFlash
        GRLG B 12 A_FireCustomMissile("TGRGrenade", 0, 1, 0, 0, 0, 3.5)
        GRLG B 0 A_ReFire
        Goto Ready 
    Flash: 
        GRLF A 3 Bright A_Light1
        GRLF B 4 Bright
        GRLF C 4 Bright A_Light2
        GRLF D 4 Bright A_Light2
        Goto LightDone
    }
}