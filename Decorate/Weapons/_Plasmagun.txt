ACTOR TGRPlasmaRifle : PlasmaRifle REPLACES PlasmaRifle
{
    Tag "Plasma Rifle"
    // Standards
    // ===========
        +Inventory.Untossable // Can not 'drop (CCMD)' the weapon
        +Inventory.Undroppable // Weapon can never be taken away from the player's inventory.
        +Weapon.NoAutoAim // Disallow AutoAim with this weapon.
        +Weapon.No_Auto_Switch // When this weapon is picked up, do not switch to it.
        Weapon.BobStyle InverseSmooth // Different taste of bobbing; use Doom Alpha's bobbing style - but smooth.

        // Because some mods in Zandronum abuse the ConsoleCommand to change the user's settings; I found one that changes bobbing....
            Weapon.BobRangeX 1.0
            Weapon.BobRangeY 1.0
            Weapon.BobSpeed  1.0
            Weapon.YAdjust 10
    // ------
    // ======

    // ----
    // Flags, Switches, and Properties
    // =================================
    +Inventory.InvBar // Show the weapon icon in the HUD Inventory list
    -Weapon.NoAutoFire // Allow the weapon to automatically fire (if the user still has +attack == true) when raised or selected.
    Weapon.PreferredSkin "Plasma-Rifle-Marine" // Zandronum feature only; dependency required: MarineSkins
    // ------
    // ======

    // ----
    // Ammo Management
    Weapon.AmmoUse 1
    Weapon.AmmoGive 40
    Weapon.AmmoType "Cell"
    // ----
    Inventory.Icon "PLASA0"
    States
    {
    Fire:
        PLSF C 0 A_GunFlash
        PLSF C 1 Bright A_FireCustomMissile ("TGRPlasmaBall")
        PLSF D 2 Bright
        TNT1 A 0 A_ReFire
        PLSF E 1 Bright
        PLSF F 1 Bright
        PLSF E 1 Bright
        PKPL B 1
        PKPL DEF 1
        PKPL G 6
        PKPL FEDC 2
        PKPL BA 1
        Goto Ready
    Hold:
        PLSF E 0 A_GunFlash
        PLSF E 1 Bright A_FireCustomMissile ("TGRPlasmaBall")
        PLSF F 1 Bright
        PLSF E 1 Bright
        PLSF C 0 A_GunFlash
        PLSF C 1 Bright A_FireCustomMissile ("TGRPlasmaBall")
        PLSF D 2 Bright
        PLSF D 0 Bright A_ReFire
        Goto Fire+3
    Flash:
        TNT1 A 1 A_Light1
        Goto LightDone
    Spawn:
        PLAS A -1
        Stop
    }
}