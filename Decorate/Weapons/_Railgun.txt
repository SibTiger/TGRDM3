Actor TGRRailgun : DoomWeapon 5012
{
    // GZDoom Builder Configuration
    // ==============================
        //$Category "Weapons"
        //$Sprite RAILA0
        //$Angled
        //$Title "Railgun"
    // ------------------------------
    // ==============================


    Game Doom
    Radius 20
    Height 16
    Weapon.Kickback 50
    Weapon.SelectionOrder 4500
    Inventory.Pickupmessage "$PICKUP_RAILGUN"
    Obituary "$OB_RAILGUN"
    Tag "Railgun"
    // Standards
    // ===========
        +Inventory.Untossable // Can not 'drop (CCMD)' the weapon
        +Inventory.Undroppable // Weapon can never be taken away from the player's inventory.
        +Weapon.NoAutoAim // Disallow AutoAim with this weapon.
        +Weapon.No_Auto_Switch // When this weapon is picked up, do not switch to it.
        Weapon.BobStyle InverseSmooth // Different taste of bobbing; use Doom Alpha's bobbing style - but smooth.
        +ForceXYBillboard // Usable only for when Voxels is disabled and using Sprites.

        // Because some mods in Zandronum abuse the ConsoleCommand to change the user's settings; I found one that changes bobbing....
            Weapon.BobRangeX 1.0
            Weapon.BobRangeY 1.0
            Weapon.BobSpeed  1.0
            Weapon.YAdjust 10
    // ------
    // ======

    // ----
    // Flags, Switches, and Properties
    // =================================
        Inventory.RespawnTics 175 // Change the respawn time to 5 seconds [Quake Live inspired]
        -Inventory.InvBar // Do not show the weapon icon in the StatusBar HUD Inventory list
        -Weapon.NoAutoFire // Allow the weapon to automatically fire (if the user still has +attack == true) when raised or selected.
        Weapon.PreferredSkin "Railgun-Marine" // Zandronum feature only; dependency required: MarineSkins
        +NoGravity
        +FloatBob
    // ------
    // ======

    // ----
    // Ammo Management
        Weapon.AmmoUse 10
        Weapon.AmmoGive 40
        Weapon.AmmoType "Cell"
        Decal RailScorch
    // ----
        Inventory.Icon "RAILA0"
    // ------
    States
    {
    Ready:
        RLGG A 1 A_WeaponReady
        Loop
    Deselect:
        RLGG A 0 A_Lower // Lower the weapon faster
        RLGG A 1 A_Lower
        Loop
    Select:
        RLGG A 0 A_Raise // Raise the weapon faster
        RLGG A 1 A_Raise
        Loop
    Fire:
        RLGG B 1 BRIGHT
        RLGG C 1 BRIGHT
        RLGG D 1 A_FireRailgun
        Goto CoolDown
    CoolDown: //Cool down state
        RLGG D 12
        RLGG C 6
        RLGG B 6
        Goto ReloadCheck
    ReloadCheck:
        RLGG F 6 A_CheckForReload(4, "ReadyToUse")
        GOTO ReloadWeapon
    ReloadWeapon:
        RLGG GHIJK 6
        RLGG L 6 A_ResetReloadCounter
        Goto ReadyToUse
    ReadyToUse:
        RLGG A 6
        RLGG M 0 A_ReFire
        Goto Ready
    Flash:
        TNT1 A 5 bright A_Light1
        TNT1 A 5 bright A_Light2
        TNT1 A 0 bright A_Light0
        Goto LightDone
    Spawn:
        // Allow a function to be called immediately
            TNT1 A 0 NoDelay
        // Spawn the item, but raise it by z-offset value
            RAIL A -1 A_Warp(AAPTR_DEFAULT, 0, 0, 8)
        Stop
    }
}