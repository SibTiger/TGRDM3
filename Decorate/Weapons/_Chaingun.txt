ACTOR TGRChaingun : Chaingun REPLACES Chaingun
{
    Tag "Chaingun"
    // Standards
    // ===========
        +Inventory.Untossable // Can not 'drop (CCMD)' the weapon
        +Inventory.Undroppable // Weapon can never be taken away from the player's inventory.
        +Weapon.NoAutoAim // Disallow AutoAim with this weapon.
        +Weapon.No_Auto_Switch // When this weapon is picked up, do not switch to it.
        Weapon.BobStyle InverseSmooth // Different taste of bobbing; use Doom Alpha's bobbing style - but smooth.

        // Because some mods in Zandronum abuse the ConsoleCommand to change the user's settings; I found one that changes bobbing....
            Weapon.BobRangeX 1.0
            Weapon.BobRangeY 1.0
            Weapon.BobSpeed  1.0
            Weapon.YAdjust 10
    // ------
    // ======

    // ----
    // Flags, Switches, and Properties
    // =================================
    +Inventory.InvBar // Show the weapon icon in the HUD Inventory list
    -Weapon.NoAutoFire // Allow the weapon to automatically fire (if the user still has +attack == true) when raised or selected.
    Weapon.PreferredSkin "Chaingun-Marine" // Zandronum feature only; dependency required: MarineSkins
    +NoGravity
    +FloatBob
    // ------
    // ======

    // ----
    // Ammo Management
    Weapon.AmmoUse 1
    Weapon.AmmoGive 80
    Weapon.AmmoType "Clip"
    Decal BulletChip
    // ----
    Inventory.Icon "MGUNA0"
    // ------
    // Sounds
    // ========
        AttackSound "weapons/chngun"
    // ------
    States
    {
    Fire:
        CHGG A 0 A_GunFlash
        CHGG A 0 BRIGHT A_FireBullets (3, 3, -1, 7, "BulletPuff", FBF_USEAMMO)
        PKCG BCD 1
        PKCG A 0 A_GunFlash
        PKCG A 1 BRIGHT A_FireBullets (3, 3, -1, 7, "BulletPuff", FBF_USEAMMO)
        PKCG BCD 1
        PKCG B 0 A_ReFire
        PKCG A 1 A_Weaponready
        PKCG BB 1 A_Weaponready
        PKCG CCC 1 A_Weaponready
        PKCG DDD 1 A_Weaponready
        Goto Ready
    Flash:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,4)
        PKCF A 1 Bright A_Light1
        PKCF B 1 Bright A_Light2
        TNT1 A 2 A_Light1
        Goto LightDone
        PKCF C 1 Bright A_Light1
        PKCF D 1 Bright A_Light2
        TNT1 A 2 A_Light1
        Goto LightDone
    Spawn:
        // Allow a function to be called immediately
            TNT1 A 0 NoDelay
        // Spawn the item, but raise it by z-offset value
            MGUN A -1 A_Warp(AAPTR_DEFAULT, 0, 0, 8)
        Stop
    }
}