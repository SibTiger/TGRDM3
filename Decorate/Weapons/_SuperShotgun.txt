ACTOR TGRSuperShotgun : SuperShotgun REPLACES SuperShotgun
{
    Tag "SuperShotgun"
    // Standards
    // ===========
        +Inventory.Untossable // Can not 'drop (CCMD)' the weapon
        +Inventory.Undroppable // Weapon can never be taken away from the player's inventory.
        +Weapon.NoAutoAim // Disallow AutoAim with this weapon.
        +Weapon.No_Auto_Switch // When this weapon is picked up, do not switch to it.
        Weapon.BobStyle InverseSmooth // Different taste of bobbing; use Doom Alpha's bobbing style - but smooth.

        // Because some mods in Zandronum abuse the ConsoleCommand to change the user's settings; I found one that changes bobbing....
            Weapon.BobRangeX 1.0
            Weapon.BobRangeY 1.0
            Weapon.BobSpeed  1.0
            Weapon.YAdjust 10
    // ------
    // ======

    // ----
    // Flags, Switches, and Properties
    // =================================
    +Inventory.InvBar // Show the weapon icon in the HUD Inventory list
    -Weapon.NoAutoFire // Allow the weapon to automatically fire (if the user still has +attack == true) when raised or selected.
    Weapon.PreferredSkin "SSG-Marine" // Zandronum feature only; dependency required: MarineSkins
    +NoGravity
    +FloatBob
    // ------
    // ======

    // ----
    // Ammo Management
    Weapon.AmmoUse 2
    Weapon.AmmoGive 20
    Weapon.AmmoType "Shell"
    Decal BulletChip
    // ----
    Inventory.Icon "SGN2A0"
    States
    {
    Ready:
        PKS2 A 1 A_WeaponReady
        Loop
    Deselect:
        PKS2 A 1 A_Lower
        Loop
    Select:
        PKS2 A 1 A_Raise
        Loop
    Fire:
        PKS2 A 3
        //This is the equivalent of 'A_FireShotgun2'
            PKS2 A 0 A_FireBullets (9.5, 7.9, 15, 5, "BulletPuff")
            PKS2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
            PKS2 A 6 A_GunFlash
        // ----
        PKS2 A 3 A_Checkreload
        PKS2 B 2
        PKS2 C 3
        PKS2 D 4
        PKS2 E 4
        PKS2 F 2 A_OpenShotgun2
        PKS2 GHIJ 2
        PKS2 K 3
        PKS2 L 2 A_LoadShotgun2
        PKS2 M 3 
        PKS2 NOPQ 2 
        PKS2 R 3 A_CloseShotgun2
        PKS2 S 3
        PKS2 T 1 A_ReFire
        Goto Ready
    Flash:
        SHT2 J 3 Bright A_Light2
        SHT2 I 2 Bright A_Light1
        Goto LightDone
    Spawn:
        // Allow a function to be called immediately
            TNT1 A 0 NoDelay
        // Spawn the item, but raise it by z-offset value
        SGN2 A -1 A_Warp (AAPTR_DEFAULT, 0, 0, 8)
        Stop
    }
}