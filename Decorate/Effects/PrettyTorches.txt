// -----------------------------------------------------------------------------
//
// Ultimate Torment 'n' Torture torch effects
//
// Made by KeksDose, May 2007, remade 13 October 2010, released 4 December 2013
//
// An effect from the lovely past which replaces the old DOOM fire animations
// on the torches with a simple particle effect. Structureless, but that is the
// point. :P
//
// Find more here:
// http://forum.zdoom.org/viewtopic.php?f=37&t=39124&p=727949#p727383
//
// -----------------------------------------------------------------------------

/* The base actors control every other flame
   or flare so be sure to base your actors off
   those here. */

Actor KD_TorchFlameBase
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	
	RenderStyle Add
	Scale 0.75
	Alpha 0.99
	States
	{
		Spawn:
			TNT1 A 0
			Goto Burn
		Burn:
			"####" AAABBBCCDDE 1 Bright A_FadeOut(0.07)
			Wait
	}
}

Actor KD_TorchFlareBase : KD_TorchFlameBase
{
	Scale 0.5
	States
	{
		Spawn:
			TNT1 A 2 Bright
			Stop
	}
}

/* The flare definitions. */

Actor KD_TorchFlareRed : KD_TorchFlareBase
{
	States
	{
		Spawn:
			RFLR A 2 Bright NoDelay A_SetTranslucent(FRandom(0.07, 0.14), 1)
			Stop
	}
}

Actor KD_TorchFlareGreen : KD_TorchFlareBase
{
	States
	{
		Spawn:
			GFLR A 2 Bright NoDelay A_SetTranslucent(FRandom(0.07, 0.14), 1)
			Stop
	}
}

Actor KD_TorchFlareBlue : KD_TorchFlareBase
{
	States
	{
		Spawn:
			BFLR A 2 Bright NoDelay A_SetTranslucent(FRandom(0.07, 0.14), 1)
			Stop
	}
}

Actor KD_BarrelFlare : KD_TorchFlareRed { Scale 1.0 }

/* The flame definitions. */

Actor KD_TorchFlameRed : KD_TorchFlameBase
{
	States
	{
		Spawn:
			FRE2 A 0
			Goto Burn
	}
}

Actor KD_TorchFlameGreen : KD_TorchFlameBase
{
	States
	{
		Spawn:
			FGRE A 0
			Goto Burn
	}
}

Actor KD_TorchFlameBlue : KD_TorchFlameBase
{
	States
	{
		Spawn:
			FBLU A 0
			Goto Burn
	}
}

Actor KD_BarrelFlame : KD_TorchFlameBase
{
	Scale 1.0
	
	States
	{
		Spawn:
			FRE2 A 0
			Goto Burn
	}
}

/* And the torch definitions. */

Actor KD_TallRedTorch : SwitchableDecoration Replaces RedTorch
{
	+SOLID
	
	Radius 16
	Height 68
	ProjectilePassHeight -16
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_TorchFlareRed", 0.0, 0.0, 54.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH A 1 A_SpawnItemEx("KD_TorchFlameRed", 0.0, FRandom(-1.0, 1.0),
									FRandom(53.0, 54.0), 0.0,
									FRandom(-0.3, 0.3), FRandom(0.3, 2.5),
									Random(0, 359), SXF_CLIENTSIDE)
			Loop
		Inactive:
			TRCH A 350
			Wait
	}
}

Actor KD_ShortRedTorch : SwitchableDecoration Replaces ShortRedTorch
{
	+Solid
	
	Radius 16
	Height 37
	
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_TorchFlareRed", 0.0, 0.0, 30.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH B 1 A_SpawnItemEx("KD_TorchFlameRed", 0.0, FRandom(-1.0, 1.0),
									FRandom(29.0, 30.0), 0.0,
									FRandom(-0.3, 0.3), FRandom(0.3, 2.5),
									Random(0, 359), SXF_CLIENTSIDE)
			Loop
		Inactive:
			TRCH B 350
			Wait
	}
}



Actor KD_TallGreenTorch : KD_TallRedTorch Replaces GreenTorch
{
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_TorchFlareGreen", 0.0, 0.0, 54.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH A 1 A_SpawnItemEx("KD_TorchFlameGreen", 0, FRandom(-1.0, 1.0),
									FRandom(53.0, 54.0), 0.0,
									FRandom(-0.3, 0.3), FRandom(0.3, 2.5),
									Random(0, 359), SXF_CLIENTSIDE)
			Loop
	}
}

Actor KD_ShortGreenTorch : KD_ShortRedTorch Replaces ShortGreenTorch
{
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_TorchFlareGreen", 0.0, 0.0, 30.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH B 1 A_SpawnItemEx("KD_TorchFlameGreen", 0, FRandom(-1.0, 1.0),
									FRandom(29.0, 30.0), 0.0,
									FRandom(-0.3, 0.3), FRandom(0.3, 2.5),
									Random(0, 359), SXF_CLIENTSIDE)
			Loop
		Inactive:
			TRCH B 350
			Wait
	}
}



Actor KD_TallBlueTorch : KD_TallRedTorch Replaces BlueTorch
{
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_TorchFlareBlue", 0.0, 0.0, 54.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH A 1 A_SpawnItemEx("KD_TorchFlameBlue", 0.0, FRandom(-1.0, 1.0),
									FRandom(53.0, 54.0), 0.0,
									FRandom(-0.3, 0.3), FRandom(0.3, 2.5),
									Random(0, 359), SXF_CLIENTSIDE)
			Loop
	}
}

Actor KD_ShortBlueTorch : KD_ShortRedTorch Replaces ShortBlueTorch
{
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_TorchFlareBlue", 0.0, 0.0, 30.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH B 1 A_SpawnItemEx("KD_TorchFlameBlue", 0.0, FRandom(-1.0, 1.0),
									FRandom(29.0, 30.0), 0.0,
									FRandom(-0.3, 0.3), FRandom(0.3, 2.5),
									Random(0, 359), SXF_CLIENTSIDE)
			Loop
		Inactive:
			TRCH B 350
			Wait
	}
}

Actor KD_BurningBarrel : SwitchableDecoration Replaces BurningBarrel
{
	+SOLID
	
	Radius 16
	Height 32
	ProjectilePassHeight -16
	States
	{
		Spawn:
		Active:
			TNT1 A 0 A_SpawnItemEx("KD_BarrelFlare", 0.0, 0.0, 28.0,
									0, 0, 0, 0, SXF_CLIENTSIDE)
			TRCH C 1 A_SpawnItemEx("KD_BarrelFlame", 0.0, FRandom(-1.0, 1.0),
									FRandom(27.0, 28.0), 0, FRandom(-0.3, 0.3),
									FRandom(0.3, 3.8), Random(0, 359),
									SXF_CLIENTSIDE)
			Loop
		Inactive:
			TRCH C 350
			Wait
	}
}